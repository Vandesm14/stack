;; See https://chromium.googlesource.com/chromiumos/docs/+/HEAD/constants/syscalls.md#x86_64-64_bit.
0 'sys-read def
1 'sys-write def
60 'sys-exit def

0 'stdin def
1 'stdout def
2 'stderr def

10 'newline def

;; Write from a buffer into a file-descriptor.
;; (buf fd -- )
'(fn! swap tou8list len swap rot rot sys-write syscall3) 'write def

;; Write from a buffer into stderr.
;; (buf -- )
'(fn! stderr write drop drop) 'eprint def
;; Write from a buffer into stdout.
;; (buf -- )
'(fn! stdout write drop drop) 'print def

;; Write from a buffer into stderr with a newline.
;; (buf -- )
'(fn! tou8list (newline) tou8list concat eprint) 'eprintln def
;; Write from a buffer into stdout with a newline.
;; (buf -- )
'(fn! tou8list (newline) tou8list concat print) 'println def

;; Read into a buffer from a file-descriptor.
;; (buf fd -- )
'(fn! swap tou8list len swap rot rot sys-read syscall3) 'read def

;; Exit the current process with a code.
;; (code -- )
'(fn! sys-exit syscall1) 'exit def
;; Exit the current process without an error code.
;; ( -- )
'(fn! 0 exit) 'exit-ok def